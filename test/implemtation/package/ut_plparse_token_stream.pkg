CREATE OR REPLACE PACKAGE ut_plparse_token_stream AS

    Description CONSTANT pete_types.typ_description := 'plparse_token_stream';

    PROCEDURE initializeWorksWithEmpty(D VARCHAR2 := 'initialize works with empty');
    PROCEDURE initializeWorksWithNonEmpty(D VARCHAR2 := 'initialize works with non empty');

    PROCEDURE currentAndConsumeItemWorks(D VARCHAR2 := 'currentItem and consume works');

    PROCEDURE rlbckWOSnapshotThrows(D VARCHAR2 := 'rollback without takeSnapshot throws');
    PROCEDURE commitWOSnapshotThrows(D VARCHAR2 := 'commit without takeSnapshot throws');
    PROCEDURE commitAfterSnapWorks(D VARCHAR2 := 'commit after takeSnapshot works');
    PROCEDURE rollbackAfterSnapWorks(D VARCHAR2 := 'rollback after takeSnapshot works');

END;
/
CREATE OR REPLACE PACKAGE BODY ut_plparse_token_stream AS

    ----------------------------------------------------------------------------
    PROCEDURE initializeWorksWithEmpty(D VARCHAR2) IS
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type();
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        pete_assert.eq(plex.tk_EOF, plparse_token_stream.currentToken().tokenType);
        pete_assert.eq(1, plparse_token_stream.getIndex);
        pete_assert.this(plparse_token_stream.eof);
        pete_assert.this(plparse_token_stream.peek(1) IS NULL);
    END;

    ----------------------------------------------------------------------------
    PROCEDURE initializeWorksWithNonEmpty(D VARCHAR2) IS
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        pete_assert.eq(plex.tk_BEGIN, plparse_token_stream.currentToken().tokenType);
        pete_assert.eq(plex.tk_End, plparse_token_stream.peek(1).tokenType);
        pete_assert.eq(plex.tk_EOF, plparse_token_stream.peek(2).tokenType);
    END;

    ----------------------------------------------------------------------------
    PROCEDURE currentAndConsumeItemWorks(D VARCHAR2) IS
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        pete_assert.eq(plex.tk_BEGIN, plparse_token_stream.currentToken().tokenType);
        pete_assert.eq(1, plparse_token_stream.getIndex);
        plparse_token_stream.consume;
        pete_assert.eq(2, plparse_token_stream.getIndex);
        pete_assert.eq(plex.tk_End, plparse_token_stream.currentToken().tokenType);
    END;

    ----------------------------------------------------------------------------
    PROCEDURE rlbckWOSnapshotThrows(D VARCHAR2) IS
        l_thrown      BOOLEAN := FALSE;
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        BEGIN
            plparse_token_stream.rollbackSnapshot;
        EXCEPTION
            WHEN OTHERS THEN
                l_thrown := TRUE;
        END;
        --
        pete_assert.this(l_thrown, 'plparse_token_stream.rollbackSnapshot should have thrown.');
    END;

    ----------------------------------------------------------------------------
    PROCEDURE commitWOSnapshotThrows(D VARCHAR2) IS
        l_thrown      BOOLEAN := FALSE;
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        BEGIN
            plparse_token_stream.commitSnapshot;
        EXCEPTION
            WHEN OTHERS THEN
                l_thrown := TRUE;
        END;
        --
        pete_assert.this(l_thrown, 'plparse_token_stream.commitSnapshot should have thrown.');
    END;

    ----------------------------------------------------------------------------
    PROCEDURE commitAfterSnapWorks(D VARCHAR2) IS
        l_thrown      BOOLEAN := FALSE;
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        plparse_token_stream.takeSnapshot;
        plparse_token_stream.consume;
        plparse_token_stream.commitSnapshot;
        -- consecutive rollback throws as stack is empty
        BEGIN
            plparse_token_stream.rollbackSnapshot;
        EXCEPTION
            WHEN OTHERS THEN
                l_thrown := TRUE;
        END;
        --
        pete_assert.this(l_thrown, 'plparse_token_stream.rollbackSnapshot should have thrown.');
    END;

    ----------------------------------------------------------------------------
    PROCEDURE rollbackAfterSnapWorks(D VARCHAR2) IS
        l_thrown      BOOLEAN := FALSE;
        l_indexBefore PLS_INTEGER;
        l_sourceLines plparse.source_lines_type := plparse.source_lines_type('begin' || chr(10), 'end');
    BEGIN
        pete.set_method_description(D);
        -- prepare
        plparse_token_stream.initialize(p_source_lines => l_sourceLines);
        --
        plparse_token_stream.takeSnapshot;
        l_indexBefore := plparse_token_stream.getIndex;
        plparse_token_stream.consume;
        plparse_token_stream.rollbackSnapshot;
        -- indexe is restored to before take snapshot
        pete_assert.eq(l_indexBefore, plparse_token_stream.getIndex);
        -- consecutive rollback throws as stack is empty
        BEGIN
            plparse_token_stream.commitSnapshot;
        EXCEPTION
            WHEN OTHERS THEN
                l_thrown := TRUE;
        END;
        --
        pete_assert.this(l_thrown, 'plparse_token_stream.commitSnapshot should have thrown.');
    END;

END;
/
